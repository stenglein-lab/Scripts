#!/usr/bin/perl

# this script parses a SAM output file
# and outputs all records that are aligned
# (i.e., all records without bit 0x04 set)
#
# Mark Stenglein April 21, 2014
#

use strict;
use Getopt::Long;
my $n_bases = 3;

my $usage = <<USAGE;

This script calculates "continuous coverage" at each position in a reference sequence from a SAM file

Outputs to stdout

Mark Stenglein April 21, 2014

 usage: $0  [h] <sam_file>

   -h                print this message

   -n num_bases      number of bases aligning 5' of a particular base in order to be counted 
                     default = $n_bases

USAGE

my $print_usage = 0;
my $output_unmapped = 0;

if (scalar @ARGV == 0 and -t STDIN) { print $usage; exit; }

GetOptions ("h" => \$print_usage, "n=i" => \$n_bases); 

if ($print_usage) { print $usage; exit; }

my $flag = 0;
my @fields = ();
my @tags = ();
my $seq_name = undef;
my $seq_length = undef;
my %histo = ();

while (<>)
{
   chomp;

   if (/^@/)
   {
      # @SQ   SN:Umbrella_L_cladeF_v1 LN:6927
      if (/\@SQ\s+SN:(\S+)\s+LN:(\d+)/)
      {
         if (defined $seq_name)
         {
            die "error - this script only supports SAM files with a single ref seq.\n";  # TODO - extend
         }
         $seq_name = $1;
         $seq_length = $2;
      }
      # don't do anything else w/ header lines
      next;
   }
   
   # split line into tab-delimited components
   @fields = split "\t";

   # is this an unmapped query?
   my $flag = $fields[1];
   # don't output unmapped queries
   # see SAM format spec.
   if ($flag & 4) { next; }
   
   my $start_pos = $fields[3];
   my $cigar = $fields[5];

   # leftmost aligned base 
   my $position_in_reference = $start_pos;

   # cigar parsing code copied from: http://davetang.org/muse/2011/01/28/perl-and-sam/
   while ($cigar !~ /^$/)
   {
      if ($cigar =~ /^([0-9]+[MIDS])/)
      {
         my $cigar_part = $1;
         # if a match
         if ($cigar_part =~ /(\d+)M/)
         {
            my $number_matches = $1;
            # only counts as continuous coverage if the previous and following N bases also aligned ...
            for (my $i = 0; $i < ($number_matches-$n_bases); $i++)
            {
               # only counts as continuous coverage if the previous and following N bases also aligned ...
               if ($i > ($n_bases - 1))
               {
                  $histo{$position_in_reference} += 1;
               }
               $position_in_reference += 1;
            }
         } 
         # if an insertion (in read), do nothing
         elsif ($cigar_part =~ /(\d+)I/)
         {
            # nada 
         } 
         # if a deletion (in read), simply jump ahead in ref
         elsif ($cigar_part =~ /(\d+)D/)
         {
            $position_in_reference += $1;
         } 
         elsif ($cigar_part =~ /(\d+)S/)
         {
            # soft clipping occurs only at ends of sequence
            # and the reported start position (field[3]), is 
            # the leftmost *aligned* base
         } 
         $cigar =~ s/$cigar_part//;
      } 
      else 
      {
         die "Unexpected cigar: $cigar\n";
      }
   }
}

foreach (my $i = 1; $i <= $seq_length; $i++)
{
   if (!$histo{$i})
   {
      $histo{$i} = 0;
   }
   print "$i\t$histo{$i}\n";
}
